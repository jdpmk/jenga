# Compute sum[i] = arr[0] + arr[1] + ... + arr[i - 1] + arr[i]
# for all 0 <= i < nA
# See: https://en.wikipedia.org/wiki/Prefix_sum
#
# Output:
# 0
# 1
# 3
# 6
# 10
# 15
# 21
# 28
# 36
# 45

alloc n as int 10 end
alloc arr as int[10] 0 end
alloc sum as int[10] 0 end

# Initialize arr[i] = i
0 while dup n -> != do
    arr over dup <-
    1 +
end drop

# Special case: sum[0] = 0
sum 0 0 <-

# Calculate for 1 <= i < n
1 while dup n -> != do
    sum over            # (1) sum[i]
    sum over 1 - ->     # (2) read sum[i - 1]
    over arr swap ->    # (3) read arr[i]
    +                   # add (2) & (3)
    <-                  # write to (1)
    1 +                 # i -> i + 1
end drop

0 while dup n -> != do
    sum over -> println
    1 +
end drop
